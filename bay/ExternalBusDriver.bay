/*!
 *  Bayrell Runtime Library
 *
 *  (c) Copyright 2016-2021 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Web.App;

use Runtime.lib;
use Runtime.BaseDriver;
use Runtime.BaseStruct;
use Runtime.Context;
use Runtime.Exceptions.ApiException;
use Runtime.Exceptions.FileNotFound;
use Runtime.Exceptions.RuntimeException;
use Runtime.Web.Message;
use Runtime.Web.App.ApiList;
use Runtime.Web.App.ApiMethod;
use Runtime.Web.App.ApiMiddleware;
use Runtime.Web.App.BusInterface;
use Runtime.Web.App.BusMiddleware;
use Runtime.Web.App.RemoteCallAnswer;
use Runtime.Web.App.RemoteCallRequest;


class ExternalBusDriver extends BaseDriver implements BusInterface
{
	
	#ifdef FRONTEND then
	
	/**
	 * Send message
	 * @return string
	 */
	async void sendMessage(Message msg)
	{
	}
	
	
	
	/**
	 * Send message
	 * @return string
	 */
	async RemoteCallAnswer remoteBusCall(RemoteCallRequest request)
	{
		string uri = request.uri;
		string app_name = request.app_name;
		string object_name = request.object_name;
		string interface_name = request.interface_name;
		string method_name = request.method_name;
		var data = request.data;
		
		string bus_gate = @.env("X-ROUTE-PREFIX", "") ~ "/api";
		string url = (uri != "") ? uri : 
			bus_gate  + "/" + app_name + "/" + object_name + '/' + interface_name + '/' + method_name + '/'
		;
		
		/* Send post */
		var res = await static::post
		{
			"url": url,
			"data": data,
		};
		
		/* Answer */
		try
		{
			res = rtl::json_decode(res);
		}
		catch (var e)
		{
			res = null;
		}
		
		if (res != null)
		{
			res = new RemoteCallAnswer(res);
		}
		else
		{
			res = new RemoteCallAnswer
			{
				"have_answer": false,
				"error_message": "Json parse error",
				"code": rtl::ERROR_PARSE_SERIALIZATION_ERROR,
				"response": null,
			};
		}
		
		return res;
	}
	
	#endif
	
	
	
	#ifdef BACKEND then
	
	Collection<ApiMethod> api_methods = null;
	
	
	/**
	 * Start driver
	 */
	public async void startDriver()
	{
		this.api_methods = Context::getSubEntities(@.entities, classof ApiList, classof ApiMethod);
	}
	
	
	
	/**
	 * Send message
	 * @return string
	 */
	async void sendMessage(Message msg)
	{
	}
	
	
	
	/**
	 * Send message
	 * @return string
	 */
	async RemoteCallAnswer remoteBusCall(RemoteCallRequest request)
	{
		RemoteCallAnswer answer = new RemoteCallAnswer
		{
			"object_name": request.object_name,
			"interface_name": request.interface_name,
			"method_name": request.method_name,
		};
		
		string app_name = request.app_name;
		
		/* Set app name */
		answer <= app_name <= @.app_name;
		
		/* Check correct app name */
		if (app_name != "self" and app_name != @.app_name)
		{
			/* Set exception */
			answer = answer::exception
			(
				answer,
				new FileNotFound
				(
					app_name,
					_("Runtime", "App"),
					rtl::ERROR_API_WRONG_APP_NAME
				)
			);
			
			/* Set 404 code */
			answer <= new_http_code <= 404;
			
			return answer;
		}
		
		/* Find api method */
		ApiMethod api_method = this.findApiMethod(request);
		if (api_method == null)
		{
			/* Set exception */
			answer = answer::exception
			(
				answer,
				new FileNotFound
				(
					request.object_name ~ "::" ~ request.interface_name ~ "::" ~ request.method_name,
					_("Runtime", "Api or method"),
					rtl::ERROR_API_NOT_FOUND
				)
			);
			
			/* Set 404 code */
			answer <= new_http_code <= 404;
			
			return answer;
		}
		
		/* Get api method */
		string class_name = api_method.class_name;
		string class_method_name = api_method.class_method_name;
		
		/* Check if method exists */
		if (not rtl::method_exists(class_name, class_method_name))
		{
			/* Set exception */
			answer = answer::exception
			(
				answer,
				new FileNotFound
				(
					request.object_name ~ "::" ~ request.interface_name ~ "::" ~ request.method_name,
					_("Runtime", "Method"),
					rtl::ERROR_API_NOT_FOUND
				)
			);
			
			/* Set 404 code */
			answer <= new_http_code <= 404;
			
			return answer;
		}
		
		/* Call api */
		try
		{
			/* Middleware */
			list res = await static::callMiddleware(request, answer, class_name, class_method_name);
			request = res[0];
			answer = res[1];
			
			fn f = rtl::method(class_name, class_method_name);
			list res = await f(request, answer);
			answer = res[1];
		}
		catch (ApiException e)
		{
			/* Set exception */
			answer = answer::exception(answer, e);
		}
		catch (RuntimeException e)
		{
			/* Set exception */
			answer = answer::exception
			(
				answer,
				new ApiException
				(
					e.getErrorMessage(),
					rtl::ERROR_FATAL
				)
			);
			
			/* Set error name */
			answer <= error_name <= e.getClassName();
		}
		
		return answer;
	}
	
	
	
	/**
	 * Render chain
	 * Call route middlewares
	 */
	static async list callMiddleware
	(
		RemoteCallRequest request, RemoteCallAnswer answer,
		string class_name, string class_method_name
	)
	{
		Collection<BusMiddleware> middlewares = [];
		
		/* Get middleware from class */
		Collection annotations = rtl::getClassAnnotations(class_name, []);
		middlewares = middlewares.concatIm
		(
			annotations.filter( lib::isInstance(classof ApiMiddleware) )
		)
		
		/* Get middleware from method */
		Dict res = rtl::getMethodsAnnotations(class_name);
		Collection annotations = res[class_method_name] |> default Collection [];
		middlewares = middlewares.concatIm
		(
			annotations.filter( lib::isInstance(classof ApiMiddleware) )
		);
		
		/* Run each middleware */
		for (int i=0; i<middlewares.count(i); i++)
		{
			BusMiddleware m = middlewares.item(i);
			Collection arr = rs::split("::", m.value);
			string m_class_name = arr.get(0, "");
			string m_method_name = arr.get(1, "");
			fn f = rtl::method(m_class_name, m_method_name);
			
			/* Run method */
			list res = await f(request, answer);
			request = res[0];
			answer = res[1];
		}
		
		return [ request, answer ];
	}
	
	
	
	/**
	 * Find api method
	 */
	ApiMethod findApiMethod(RemoteCallRequest request)
	{
		Collection<ApiMethod> api_methods = this.api_methods;
		string object_name = request.object_name;
		string interface_name = request.interface_name;
		string method_name = request.method_name;
		for (int i=0; i<api_methods.count(); i++)
		{
			ApiMethod api_method = api_methods[i];
			if
			(
				api_method.object_name == object_name and
				api_method.interface_name == interface_name and
				api_method.method_name == method_name
			)
			{
				return api_method;
			}
		}
		return null;
	}
	
	#endif
}


#ifcode ES6 then
Object.assign(Runtime.Web.App.ExternalBusDriver,
{
	/**
	 * Send post. Returns json object or null if error
	 */
	post: async function(ctx, obj)
	{
		var url = obj.get(ctx, "url");
		var data = obj.get(ctx, "data");
		
		/* Build pos data */
		var build_data = this.buildData(ctx, data);
		var post_data = this.buildPostData(ctx, build_data);
		
		/* Send post */
		var xhr = await this.sendPost(ctx, url, post_data);
		return xhr.responseText;
	},
	
	
	
	/**
	 * Convert data to Native for ajax POST request
	 * @params serializable data
	 * @return Vector
	 */
	buildData: function(ctx, data)
	{
		var res = [];
		data = Runtime.RuntimeUtils.json_encode(ctx, data);
		/*json = btoa( unescape(encodeURIComponent(json)) );*/
		res.push({"key": "data", "value": data});
		return res;
	},
	
	
	
	/**
	 * Returns FormData
	 * @params data - json object
	 * @return FormData
	 */
	buildPostData: function(ctx, data)
	{
		var post_data = new FormData();
		
		/* Add data to post data */
		for (var i=0; i<data.length; i++)
		{
			var obj = data[i];
			var key = obj.key;
			var val = obj.value;
			if (val instanceof FileList)
			{
				for (var i=0; i<val.length; i++)
				{
					post_data.append(key + "[]", val.item(i), val.item(i).name);
				}
			}
			else if (val instanceof File)
			{
				post_data.append(key, val, val.name);
			}
			else
			{
				post_data.append(key, val);
			}
		}
		
		return post_data;
	},
	
	
	
	/**
	 * Send api request
	 * @param string class_name
	 * @param string method_name
	 * @param Map<string, mixed> data
	 * @param callback f
	 */ 
	sendPost: async function(ctx, url, post_data)
	{
		return await new Promise((resolve, reject) =>{
			try
			{
				var xhr = new XMLHttpRequest();
				xhr.open('POST', url, true);
				xhr.send(post_data);
				xhr.onreadystatechange = (function(ctx, xhr, resolve, reject) {
					return function()
					{
						if (xhr.readyState != 4) return;
						if (xhr.status == 200)
						{
							resolve(xhr);
						}
						else
						{
							reject
							(
								new Runtime.Exceptions.RuntimeException
								(ctx, xhr.status + " " + xhr.statusText, xhr.status) 
							);
						}
					}
				})(ctx, xhr, resolve, reject);
			}
			catch (e)
			{
				reject(e);
			}
		});
	},
	
});
#endif